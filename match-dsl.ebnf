

expression           = literal
                     | identifier
                     | operation
                     | delimited expression;

delimited expression = "(", expression, ")";

(* operations *)
operation            = comparison operation
                     | binary operation
                     | unary operation;

comparison operation = expression, comparison operator, expression,
                     { comparison operator, expression }
unary operation      = unary operator, expression;
binary operation     = expression, binary operator, expression;

(* operators *)
unary operator       = "not";
binary operator      = "and" | "or";
comparison operator  = ">" | "<" | "=" | ">=" | "<=";

(* identifiers *)
identifier           = lowercase, { lowercase | digit };

(* literals *)
literal              = number
                     | boolean
                     | string
                     | resolution;

number               = nonzero digit, { digit };
boolean              = "true" | "false";
string               = '"', { text }, '"';
resolution           = number, "x", number, [ ":", number ];

(* definitions *)
text                 = escaped character | ? any valid UTF-8 sequence ?
escaped character    = '\"' | "\\" | "\r" | "\n" | "\t"
digit                = "0" | nonzero digit;
nonzero digit        = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";

lowercase            = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i"
                     | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
                     | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z";

uppercase            = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I"
                     | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R"
                     | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z";